(define (domain hospitaldomain)
  (:requirements :fluents :negative-preconditions :typing )
  (:types bed patient specialty care gender origin roomtype birthtype stay age )
  
  (:predicates   
    ;Bed
    (bedstay ?varbed - bed ?varstay - stay)
    (bedroomtype ?varbed - bed ?varroomtype - roomtype)
    (bedorigin ?varbed - bed ?varorigin - origin)
    (bedgender ?varbed - bed ?vargender - gender)
    (bedage ?varbed - bed ?varage - age)
    (bedbirthtype ?varbed - bed ?varbirthtype - birthtype)
    (bedcare ?varbed - bed ?varcare - care)
    (bedspecialty ?varbed - bed ?varspecialty - specialty)
    (bedisolation ?varbed - bed)
    (bedfree ?varbed - bed)
    (busybed ?varbed - bed)
    ;Effect
    (in ?p - patient ?varbed - bed)
    ;Patient
    (patientstay ?p - patient ?varstay - stay)
    (patientroomtype ?p - patient ?varroomtype - roomtype)
    (patientorigin ?p - patient ?varorigin - origin)
    (patientgender ?p - patient ?vargender - gender)
    (patientage ?p - patient ?varage - age)
    (patientbirthtype ?p - patient ?varbirthtype - birthtype)
    (patientcare ?p - patient ?varcare - care)
    (patientspecialty ?p - patient ?varspecialty - specialty)
    (patientisolation ?p - patient)
    (allocated ?p - patient)
    (donotallocate ?p - patient)
    ;Specialties
    (patientuti ?p - patient)
    (bedobstetricia ?varbed - bed)
    (patientobstetricia ?p - patient)
    (beducl ?varbed - bed)
    (patientucl ?p - patient)
    (bedavc ?varbed - bed)
    (patientavc ?p - patient)
    (bedpsiquiatria ?varbed - bed)
    (patientpsiquiatria ?p - patient)
    (bedcirurgiabariatrica ?varbed - bed)
    (patientcirurgiabariatrica ?p - patient)
    (bedginecologia ?varbed - bed)
    (patientginecologia ?p - patient)
  )

  (:action allocateuti
    :parameters (?p - patient)
    :precondition (and (not (allocated ?p)) 
                       (patientuti ?p))
    :effect (and (donotallocate ?p))
  )
  
  (:action allocateisolation
    :parameters (?p - patient ?varbed - bed)
    :precondition (and (not (allocated ?p)) 
                       (bedfree ?varbed)
                       (patientisolation ?p))
    :effect (and (not (bedfree ?varbed)) (in ?p ?varbed) 
                 (allocated ?p) (busybed ?varbed) 
                 (bedisolation ?varbed))
  )

  (:action allocateobstetricia
    :parameters (?p - patient ?varbed - bed ?varbirthtype - birthtype )
    :precondition (and (not (allocated ?p)) 
                       (bedfree ?varbed) 
                       (patientobstetricia ?p)
                       (bedobstetricia ?varbed)
                       (bedbirthtype ?varbed ?varbirthtype)
                       (patientbirthtype ?p ?varbirthtype)
                       )
    :effect (and (not (bedfree ?varbed)) (in ?p ?varbed) 
                 (allocated ?p) (busybed ?varbed))
  )

  (:action allocateucl
    :parameters (?p - patient ?varbed - bed ?varage - age)
    :precondition (and (not (allocated ?p)) 
                       (bedfree ?varbed) 
                       (patientucl ?p)
                       (beducl ?varbed)
                       (patientage ?p ?varage)
                       (bedage ?varbed ?varage)
                       )
    :effect (and (not (bedfree ?varbed)) (in ?p ?varbed) 
                 (allocated ?p) (busybed ?varbed))
  )

  (:action allocateavc
    :parameters (?p - patient ?varbed - bed ?vargender - gender )
    :precondition (and (not (allocated ?p)) 
                       (bedfree ?varbed) 
                       (patientavc ?p)
                       (bedavc ?varbed)
                       (patientgender ?p ?vargender)
                       (bedgender ?varbed ?vargender)
                       )
    :effect (and (not (bedfree ?varbed)) (in ?p ?varbed) 
                 (allocated ?p) (busybed ?varbed))
  )

  (:action allocatepsiquiatria
    :parameters (?p - patient ?varbed - bed ?vargender - gender )
    :precondition (and (not (allocated ?p)) 
                       (bedfree ?varbed) 
                       (patientpsiquiatria ?p)
                       (bedpsiquiatria ?varbed)
                       (patientgender ?p ?vargender)
                       (bedgender ?varbed ?vargender)
                       )
    :effect (and (not (bedfree ?varbed)) (in ?p ?varbed) 
                 (allocated ?p) (busybed ?varbed))
  )

  (:action allocatecirurgiabariatrica
    :parameters (?p - patient ?varbed - bed ?vargender - gender )
    :precondition (and (not (allocated ?p)) 
                       (bedfree ?varbed) 
                       (patientcirurgiabariatrica ?p)
                       (bedcirurgiabariatrica ?varbed)
                       (patientgender ?p ?vargender)
                       (bedgender ?varbed ?vargender)
                       )
    :effect (and (not (bedfree ?varbed)) (in ?p ?varbed) 
                 (allocated ?p) (busybed ?varbed))
  )

  (:action allocateginecologia
    :parameters (?p - patient ?varbed - bed ?varroomtype - roomtype )
    :precondition (and (not (allocated ?p)) 
                       (bedfree ?varbed) 
                       (patientginecologia ?p)
                       (bedginecologia ?varbed)
                       (patientroomtype ?p ?varroomtype)
                       (bedroomtype ?varbed ?varroomtype)
                       )
    :effect (and (not (bedfree ?varbed)) (in ?p ?varbed) 
                 (allocated ?p) (busybed ?varbed))
  )

  (:action allocate
    :parameters (?p - patient ?varbed - bed ?varspecialty - specialty
                 ?varstay - stay ?varroomtype - roomtype ?varorigin - origin
                 ?vargender - gender ?varage - age ?varcare - care )
    :precondition (and (not (allocated ?p)) 
                       (bedfree ?varbed) 
                       (patientspecialty ?p ?varspecialty) 
                       (patientstay ?p ?varstay)
                       (patientroomtype ?p ?varroomtype)
                       (patientorigin ?p ?varorigin)
                       (patientgender ?p ?vargender)
                       (patientage ?p ?varage)
                       (patientcare ?p ?varcare)
                       (bedspecialty ?varbed ?varspecialty)
                       (bedstay ?varbed ?varstay)
                       (bedroomtype ?varbed ?varroomtype)
                       (bedorigin ?varbed ?varorigin)
                       (bedgender ?varbed ?vargender)
                       (bedage ?varbed ?varage)
                       (bedcare ?varbed ?varcare)
                  )
    :effect (and (not (bedfree ?varbed)) (in ?p ?varbed) 
                 (allocated ?p) (busybed ?varbed))
  )
)
